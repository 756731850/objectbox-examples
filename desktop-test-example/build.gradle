// Repositories are defined in root build.gradle

buildscript {
    ext {
        osName = System.getProperty("os.name").toLowerCase()
        isLinux = osName.contains("linux")
        isWindows = osName.contains("windows")
        is64 = System.getProperty("sun.arch.data.model") == "64"
        isLinux64 = isLinux && is64
        isWindows64 = isWindows && is64
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.12"
    }
}

apply plugin: 'java'
apply plugin: 'net.ltgt.apt-idea'
apply plugin: 'io.objectbox'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

dependencies {
    compile "io.objectbox:objectbox-java:$objectboxVersion"
    // Could also be Android dependency:
    // compile "io.objectbox:objectbox-android:$objectboxVersion"

    if (isLinux64) {
        compile "io.objectbox:objectbox-linux:$objectboxVersion"
    } else if (isWindows64) {
        compile "io.objectbox:objectbox-windows:$objectboxVersion"
    } else println("Native dependency could not be set up for unsupported OS: " + osName)

    testCompile 'junit:junit:4.12'
}